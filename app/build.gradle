apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            // Only configure signing if not on JitPack
            if (!System.env.JITPACK) {
                keyAlias 'key0'
                keyPassword 'candybar'
                storeFile file('candybar.jks')
                storePassword 'candybar'
            }

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    compileSdk rootProject.ext.CompileSdk

    namespace 'com.candybar.dev'

    defaultConfig {
        applicationId 'com.candybar.dev'
        minSdkVersion rootProject.ext.MinSdk
        targetSdkVersion rootProject.ext.TargetSdk
        versionCode 1
        versionName '1.0'

        // This code loads the license key from `local.properties` file
        // and saves it as build config field named "LICENSE_KEY"
        String licenseKey = ''
        Properties props = new Properties()
        File propFile = rootProject.file('local.properties')
        if (propFile.exists()) {
            props.load(propFile.newDataInputStream())
            licenseKey = props.get('license_key', '')
        }
        buildConfigField 'String', 'LICENSE_KEY', "\"$licenseKey\""
    }

    buildTypes {
        release {
            if (!System.env.JITPACK) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig = true
    }

    lint {
        abortOnError false
    }
}

configurations.all {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

dependencies {
    // CandyBar main library
    implementation 'com.github.SamNeill.Candybar-Amazon:candybar:v3.20.3-amazon'
    // PreLollipopTransitions module
    implementation 'com.github.SamNeill.Candybar-Amazon:PreLollipopTransitions:v3.20.3-amazon'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'
}
